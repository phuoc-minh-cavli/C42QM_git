#===============================================================================
#
# LTE ML1 AFC Scons
#
# GENERAL DESCRIPTION
#    build script
#
# Copyright (c) 2010 by Qualcomm Technologies, Incorporated.
# All Rights Reserved.
# QUALCOMM Proprietary/GTDR
#
#-------------------------------------------------------------------------------
#
#  $Header: $
#
#                      EDIT HISTORY FOR FILE
#
#  This section contains comments describing changes made to the module.
#  Notice that changes are listed in reverse chronological order.
#
# when       who     what, where, why
# --------   ---     ---------------------------------------------------------
# 11/21/10   cguo    Initial version.
#===============================================================================
Import('env')
env = env.Clone()

if 'USES_FEATURE_CATM1_DISABLE' in env:
    Return()

from glob import glob
from os.path import join, basename

#-------------------------------------------------------------------------------
# Setup source PATH
#-------------------------------------------------------------------------------
SRCPATH = "../src"

env.VariantDir('${BUILDPATH}', SRCPATH, duplicate=0)

#-------------------------------------------------------------------------------
# Set MSG_BT_SSID_DFLT for legacy MSG macros
#-------------------------------------------------------------------------------
env.Append(CPPDEFINES = [
   "MSG_BT_SSID_DFLT=MSG_SSID_LTE_ML1",
])

#-------------------------------------------------------------------------------
# Necessary Public and Restricted API's
#-------------------------------------------------------------------------------
env.RequirePublicApi([
    'HWENGINES',
    'DEBUGTOOLS',
    'SERVICES',
    'SYSTEMDRIVERS',
    'DAL',
    'POWER',
    'BUSES',
    'MPROC',
    'KERNEL',                             # needs to be last 
    ], area='core')

#env.RequirePublicApi(['MPROC'])
env.RequirePublicApi('QTF', area='mob') 

# Need to get access to Modem Public headers
env.RequirePublicApi([
    'ONEX',
    'GPS',      
    'HDR',      
    'MCS',
    'MMODE',    
    'RFA',      
    'UIM',      
    'UTILS',      
    ])

# Need get access to Modem Restricted headers
env.RequireRestrictedApi([
    'MMODE',    
    'NAS',      
    'RFA',      
    'MDSP',     
    'MDSPSVC',  
    'GERAN',    
    'GPS',      
    'ONEX',     
    'HDR',      
    'MCS',
    'LTE',
    'FW',
    'UTILS',
    'MODEM',
    ])

# Need to get access to LTE Protected headers
env.RequireProtectedApi([
    'LTE',
    'LTE_POI',
    ])

env.PublishPrivateApi('AFC', '${INC_ROOT}/modem/lte/ML1/afc/src')

#-------------------------------------------------------------------------------
# Generate the library and add to an image
#-------------------------------------------------------------------------------

# Construct the list of source files by looking for *.c
LTE_ML1_AFC_SOURCES = ['${BUILDPATH}/' + basename(fname)
                   for fname in glob(join(env.subst(SRCPATH), '*.c'))]

env.STM2('${BUILDPATH}/__lte_ml1_afc_stm_int.c',
         '${BUILDPATH}/lte_ml1_afc_stm.stm')
env.Append(CPPPATH = '${BUILDPATH}')
env.Append(CPPPATH = '${LTE_ROOT}/ML1/afc/build/${BUILDPATH}')
LTE_ML1_AFC_SOURCES.append('${BUILDPATH}/__lte_ml1_afc_stm_int.c')

env.AddBinaryLibrary(['MODEM_PAGEONLY'], '${BUILDPATH}/lte_ml1_afc', LTE_ML1_AFC_SOURCES, pack_exception=['LTE_ML1_AFC_SOURCES'])

if 'USES_FEATURE_DYNAMIC_LOADING_LTE' not in env:
    #existing to compile module as static library with env.AddBinaryLibrary()
    if 'USES_ALLOW_REGENERATE_LIBS' in env:
        # Compile the source and create a library (ship source)
        env.AddLibrary(['MODEM_MODEM', 'MOB_LTE','MODEM_PAGEONLY'], '${BUILDPATH}/lte_ml1_afc', LTE_ML1_AFC_SOURCES)
    else:
        # Compile the source and convert to a binary library
        env.AddBinaryLibrary(['MODEM_MODEM', 'MOB_LTE','MODEM_PAGEONLY'], '${BUILDPATH}/lte_ml1_afc', LTE_ML1_AFC_SOURCES, pack_exception=['USES_CUSTOMER_GENERATE_LIBS'])
else:
    if 'USES_FEATURE_EXTERNAL_PAGING' in env:
      env.AddBinaryLibrary(['MODEM_MODEM', 'MOB_LTE','MODEM_PAGEONLY'], '${BUILDPATH}/lte_ml1_afc_dmm', LTE_ML1_AFC_SOURCES, pack_exception=['USES_CUSTOMER_GENERATE_LIBS'])
    else:
      SHARED_LIBRARY='m1'
      env.Append(CFLAGS='-DDYNAMIC_LINKING_ENABLED')
      #add dynamic sources as shared library 
      env.AddSharedLibrary(['MODEM_MODEM', 'MOB_LTE','MODEM_PAGEONLY'], '${BUILDPATH}/lte_ml1_afc', LTE_ML1_AFC_SOURCES, pack_exception=['USES_CUSTOMER_GENERATE_LIBS'])
           
#-------------------------------------------------------------------------------
# avoid run-time errors when trying to send MSGR messages 
#-------------------------------------------------------------------------------

if 'USES_MSGR' in env:
    env.AddUMID('${BUILDPATH}/lte_ml1_afc.umid', ['../inc/lte_ml1_afc_msg.h'])

#-------------------------------------------------------------------------------
# Look for test build files
#-------------------------------------------------------------------------------
env.LoadSoftwareUnits()
