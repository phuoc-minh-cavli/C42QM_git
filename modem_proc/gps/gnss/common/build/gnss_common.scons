# -------------------------------------------------------------------------------- #
#                     G N S S _ C O M M O N . S C O N S
#
# DESCRIPTION
#   SCons file for the GNSS Common subsystem.
#
#
# INITIALIZATION AND SEQUENCING REQUIREMENTS
#   None.
#
# Copyright (c) 2010 - 2012 Qualcomm Technologies Incorporated.
# Qualcomm Confidential and Proprietary. All Rights Reserved.
# Copyright (c) 2013 - 2014 Qualcomm Atheros, Inc.
# Qualcomm Atheros Confidential and Proprietary. All Rights Reserved. 
#
# Export of this technology or software is regulated by the U.S. Government.
# Diversion contrary to U.S. law prohibited.
#
# All ideas, data and information contained in or disclosed by
# this document are confidential and proprietary information of
# Qualcomm Technologies Incorporated and all rights therein are expressly reserved.
# By accepting this material the recipient agrees that this material
# and the information contained therein are held in confidence and in
# trust and will not be used, copied, reproduced in whole or in part,
# nor its contents revealed in any manner to others without the express
# written permission of Qualcomm Technologies Incorporated.
# --------------------------------------------------------------------------------- #

# --------------------------------------------------------------------------------- #
#
#                      EDIT HISTORY FOR FILE
#
# This section contains comments describing changes made to this file.
# Notice that changes are listed in reverse chronological order.
#
# $Header: //components/rel/gnss8.mpss/9.5.1.1/gnss/common/build/gnss_common.scons#1 $
# $DateTime: 2020/07/21 21:23:44 $
# $Author: pwbldsvc $ 
# 
# when         who     what, where, why
# ---------    ---     ------------------------------------------------------------
# 10/06/10     ah      Initial Version
#
# --------------------------------------------------------------------------------- #

#--------------------------------------------------------------------------------
# Import the SCons environment
#--------------------------------------------------------------------------------
Import('gps_env')
from glob import glob
from os.path import join, basename

#--------------------------------------------------------------------------------
# Check USES flags and return if library isn't needed
#--------------------------------------------------------------------------------
if 'USES_CGPS' not in gps_env:
    Return()

#--------------------------------------------------------------------------------
# Use Legacy NV API for certain product lines
#--------------------------------------------------------------------------------
if ( 'USES_GNSS_SA' in gps_env or
     'USES_GPS_LM_STANDALONE'in gps_env or
     gps_env['PRODUCT_LINE'] in [ 'MPSS.BO.2.5',
                              'MPSS.BO.2.6',
                            ]
   ):
    gps_env.PrintInfo("Use Legacy NV API")
    gps_env.Append(CPPDEFINES = " FEATURE_CGPS_LEGACY_NV_API")

#--------------------------------------------------------------------------------
# Setup source PATH
#--------------------------------------------------------------------------------
SRCPATH = '../src'
gps_env.VariantDir('${BUILDPATH}', SRCPATH, duplicate=0)

#--------------------------------------------------------------------------------
# Name of the subsystem to which this unit belongs
#--------------------------------------------------------------------------------
LIB_TARGET = '${BUILDPATH}/' + 'gnss_common'

#--------------------------------------------------------------------------------
# Images that this VU is added
#--------------------------------------------------------------------------------
IMAGES = ['MODEM_MODEM']

#--------------------------------------------------------------------------------
# Generate the library and add to an image
#--------------------------------------------------------------------------------
# Most source files in gnss\common are not to be shipped
# Put all source files into no-ship list
BINARY_LIB_SOURCES = gps_env.GenerateCompileList(SRCPATH, '*.c')
gps_env.RemoveCompileFile(BINARY_LIB_SOURCES, 'gps_nv_efs_upos.c')

if 'USES_GNSS_SA' in gps_env:
    gps_env.RemoveCompileFile(BINARY_LIB_SOURCES, 'gnss_cs_protocol.c')

if 'USES_GPS_LM_STANDALONE' in gps_env:
    gps_env.RemoveCompileFile(BINARY_LIB_SOURCES, 'gnss_cs_protocol.c')
	
# Make the list of ok-to-ship source files (per wiki page)
SHIPPABLE_FILES = ['gnss_dm_log.c',
                   'gnss_persistent_cfg.c',
                   'gnss_serdes.c',
                   'gps_fs_task.c',
                   'gps_nv_efs.c',
                   'gps_nv_loader.c',
                   'gps_rom_almanac.c',
                   'gnss_common_nv.c',
                  ]
LIB_SOURCES = gps_env.GenerateCompileList(SRCPATH, SHIPPABLE_FILES)

# Now update list of no-ship files
gps_env.RemoveCompileFile(BINARY_LIB_SOURCES, SHIPPABLE_FILES)

#--------------------------------------------------------------------------------
# Add our library to the Modem image
#--------------------------------------------------------------------------------
gps_env.AddLibrary(IMAGES, LIB_TARGET, LIB_SOURCES)

#--------------------------------------------------------------------------------
# If Island mode is supported, tag necessary files and add the compile flags
#--------------------------------------------------------------------------------
if 'USES_GNSS_ISLAND_LINK' in gps_env:
    # Find all files that end with 'uimage.c'
    COMMON_ISLAND_SOURCES = gps_env.GenerateCompileList(SRCPATH, '*uimage.c')
    
    gps_env.RemoveCompileFile(BINARY_LIB_SOURCES, COMMON_ISLAND_SOURCES)
    
    # Turn off debug symbols
    # Avoid island symbols in .sdata/.sbss
    gps_env.Append(CCFLAGS = " -G0 ")
    
    #We don't support island functions with '-fstack-protector' or '-fstack-protector-strong'
    if "-fstack-protector" in gps_env['HEXAGON_STKPRO'] or \
       "-fstack-protector-strong" in gps_env['HEXAGON_STKPRO']:
        gps_env.Replace(HEXAGON_STKPRO = "")
    
    # Generate the library for the ISLAND source files.
    common_uimage_libs = gps_env.AddGPSFullBinaryLibrary(IMAGES, (LIB_TARGET + '_uimage'), COMMON_ISLAND_SOURCES, pack_exception=['USES_CUSTOMER_GENERATE_LIBS'])
    
    # Tag the generated library for inclusion in the ISLAND section.
    gps_env.AddGPSFullBinaryLibrary(IMAGES, (LIB_TARGET + '_pvt'), BINARY_LIB_SOURCES, pack_exception=['USES_CUSTOMER_GENERATE_LIBS'])
    gps_env.AddIslandLibrary(IMAGES, common_uimage_libs)
else:
    #--------------------------------------------------------------------------------
    # Ship our code as binary library and add it to the Modem image
    #--------------------------------------------------------------------------------
    gps_env.AddGPSFullBinaryLibrary(IMAGES, (LIB_TARGET + '_pvt'), BINARY_LIB_SOURCES, pack_exception=['USES_CUSTOMER_GENERATE_LIBS'])

#--------------------------------------------------------------------------------
# Initialize GPS_FS Task through RCINIT
#--------------------------------------------------------------------------------
RCINIT_TASK_FN = {
    'thread_name'         : 'gps_fs',
    'thread_entry'        : 'gps_fs_task',
    'stack_size_bytes'    : gps_env.subst('$GPS_FS_STKSZ'),
    'sequence_group'      : gps_env.subst('$MODEM_UPPERLAYER'),
    'priority_amss_order' : 'GPS_FS_PRI_ORDER',
    'cpu_affinity'        : 'REX_ANY_CPU_AFFINITY_MASK',
    'policy_optin'        : ['default', 'ftm', ],
}
gps_env.AddRCInitTask (IMAGES, RCINIT_TASK_FN)

