#===============================================================================
#
# SLPC Scons
#
# GENERAL DESCRIPTION
#    build script
#
# Copyright (c) 2019 Qualcomm Technologies Incorporated.
# All Rights Reserved.
# Qualcomm Confidential and Proprietary

# Export of this technology or software is regulated by the U.S. Government.
# Diversion contrary to U.S. law prohibited.

# All ideas, data and information contained in or disclosed by
# this document are confidential and proprietary information of
# Qualcomm Technologies Incorporated and all rights therein are expressly reserved.
# By accepting this material the recipient agrees that this material
# and the information contained therein are held in confidence and in
# trust and will not be used, copied, reproduced in whole or in part,
# nor its contents revealed in any manner to others without the express
# written permission of Qualcomm Technologies Incorporated.
#
#-------------------------------------------------------------------------------
#
#  $Header: //components/rel/mcs.mpss/5.1/slpc/build/slpc.scons#8 $
#
#===============================================================================
Import('env')
env = env.Clone()

#-------------------------------------------------------------------------------
# Exit immediately for PLT builds
#-------------------------------------------------------------------------------
if 'USES_PLT' in env:
    Return()

#-------------------------------------------------------------------------------
# Setup source PATH
#-------------------------------------------------------------------------------
SRCPATH = ".."
env.VariantDir('${BUILDPATH}', SRCPATH, duplicate=0)

#-------------------------------------------------------------------------------
# Set MSG_BT_SSID_DFLT for legacy MSG macros
#-------------------------------------------------------------------------------
env.Append(CPPDEFINES = [
    "MSG_BT_SSID_DFLT=MSG_SSID_SLEEP",
])

#env.Append(CPPDEFINES=[('SLPC_TEST',1)])

#-------------------------------------------------------------------------------
# Define the number of slpc worker threads.
#-------------------------------------------------------------------------------
SLPC_NUM_THREADS = 2
env.Append(CPPDEFINES="SLPC_NUM_THREADS="+str(SLPC_NUM_THREADS))

#-------------------------------------------------------------------------------
# RCInit
#-------------------------------------------------------------------------------

if 'USES_RCINIT' in env:
  RCINIT_IMG = ['MODEM_MODEM', 'MOB_MCS_SLPC', 'MODEM_PAGEONLY']
  env.AddRCInitFunc(
  RCINIT_IMG,
  {
    'sequence_group'             : 'RCINIT_GROUP_3',
    'init_name'                  : 'slpc_init',
    'init_function'              : 'slpc_init',
    'dependencies'               : ['timer','GPIOInt'],
    'policy_optin'               : ['default', 'ftm', ],
  })
  for thr_num in range(SLPC_NUM_THREADS):
    env.AddRCInitTask(
    RCINIT_IMG,
    {
     'sequence_group'             : 'RCINIT_GROUP_3',
     'thread_name'                : 'slpc_worker_'+str(thr_num),
     'thread_entry'               : 'slpc_task_common',
     'stack_size_bytes'           : '4096',
     'priority_amss_order'        : 'SLPC_PRI_ORDER',
     'cpu_affinity'               : 'REX_ANY_SMT_MASK',
     'policy_optin'               : ['default', 'ftm', ],
    })

#-------------------------------------------------------------------------------
# Generate the library and add to an image
#-------------------------------------------------------------------------------

SLPC_SRC = [
    '${BUILDPATH}/src/slpc.c',
    '${BUILDPATH}/src/slpc_trace.c'
]

# Compile the slpc source and convert to a library
env.AddLibrary(['MODEM_MODEM', 'MOB_MCS_SLPC', 'MODEM_PAGEONLY'],
               '${BUILDPATH}/slpc',
               SLPC_SRC)

#-------------------------------------------------------------------------------
# Test code
#-------------------------------------------------------------------------------

if 'USES_SLPC_TEST' in env and 'USES_RCINIT' in env:
    RCINIT_IMG = ['MODEM_MODEM', 'MOB_MCS_SLPC_UT', 'MODEM_PAGEONLY']
    env.AddRCInitTask(
    RCINIT_IMG,
    {
      'sequence_group'             : 'RCINIT_GROUP_7',
      'thread_name'                : 'slpc_test1',
      'stack_size_bytes'           : '4096',
      'priority_amss_order'        : 'GSM_L1_PRI_ORDER',
      'cpu_affinity'               : 'REX_ANY_SMT_MASK'
    })
    env.AddRCInitTask(
    RCINIT_IMG,
    {
      'sequence_group'             : 'RCINIT_GROUP_7',
      'thread_name'                : 'slpc_test2',
      'stack_size_bytes'           : '4096',
      'priority_amss_order'        : 'GSM_L1_PRI_ORDER',
      'cpu_affinity'               : 'REX_ANY_SMT_MASK'
    })

    SLPC_TEST_SRC = [
        '${BUILDPATH}/src/slpc_stubs.c',
        '${BUILDPATH}/src/slpc_test.c',
        '${BUILDPATH}/src/slpc_test_output.c',
        '${BUILDPATH}/src/slpc_test_test1.c',
        '${BUILDPATH}/src/slpc_test_test2.c',
        '${BUILDPATH}/src/slpc_test_test3.c',
        '${BUILDPATH}/src/slpc_test_test4.c',
        '${BUILDPATH}/src/slpc_test_test5.c',
    ]

    env.AddLibrary(['MODEM_MODEM', 'MOB_MCS_SLPC_UT', 'MODEM_PAGEONLY'],
                   '${BUILDPATH}/slpc_test', SLPC_TEST_SRC)
