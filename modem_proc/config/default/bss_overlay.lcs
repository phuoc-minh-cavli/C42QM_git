  /*========================================================================
     BSS overlay.
     Share this memory region between buffers which will never coexist. 
     These are also overwritten on power collapse and fatal error, due to 
     memory dump - so do not place anything here which is required for debug.
    ======================================================================== */
  __bss_overlay_size = 0;
  __bss_overlay_start = . ;
  
  .bss.overlay.fw __bss_overlay_start : 
  {
    *(.bss.overlay.common_fw)
    . = ALIGN(64);
    __hw_demback_buffer_start__ = .;
    . += __hw_demback_buffer_size__;  /* Allocate memory */
    . = ALIGN(64);
    __hw_demback_buffer_end__ = .;
    __bss_overlay_size = MAX(__bss_overlay_size, . - __bss_overlay_start);

    . = __bss_overlay_start;
    *(.bss.overlay.c2k)
    __bss_overlay_size = MAX(__bss_overlay_size, . - __bss_overlay_start);

    . = __bss_overlay_start;
    *(.bss.overlay.geran)
    __bss_overlay_size = MAX(__bss_overlay_size, . - __bss_overlay_start);

    . = __bss_overlay_start;
    *(.bss.overlay.lte)  
    __bss_overlay_size = MAX(__bss_overlay_size, . - __bss_overlay_start);

    . = __bss_overlay_start;
    *(.bss.overlay.tdscdma)
    __bss_overlay_size = MAX(__bss_overlay_size, . - __bss_overlay_start);

    . = __bss_overlay_start;
    *(.bss.overlay.wcdma)  
    __bss_overlay_size = MAX(__bss_overlay_size, . - __bss_overlay_start);

    . = __bss_overlay_start;
    *(.bss.overlay.ftm)  
    __bss_overlay_size = MAX(__bss_overlay_size, . - __bss_overlay_start);    

  . = __bss_overlay_start + __bss_overlay_size ;   /* Max size of all */
  } :BSS_OVERLAY
  
 
  . = ALIGN (64);

