#===============================================================================
#
# Copyright (c) 2016 Qualcomm Technologies Incorporated.
#
# All Rights Reserved. Qualcomm Confidential and Proprietary
# Export of this technology or software is regulated by the U.S. Government.
# Diversion contrary to U.S. law prohibited.
#
# All ideas, data and information contained in or disclosed by
# this document are confidential and proprietary information of
# Qualcomm Technologies Incorporated and all rights therein are expressly reserved.
# By accepting this material the recipient agrees that this material
# and the information contained therein are held in confidence and in
# trust and will not be used, copied, reproduced in whole or in part,
# nor its contents revealed in any manner to others without the express
# written permission of Qualcomm Technologies Incorporated.
#
#-------------------------------------------------------------------------------
#
#  $Header: //source/qcom/qct/modem/fw/components/fw_nb.mpss/rel/15.2/release/build/fw_nb.scons#2 $
#
#===============================================================================

# --------------------------------------------------------------------------- #
# Import SCons Environment                                                    #
# --------------------------------------------------------------------------- #
import os
Import('env')
from glob import glob
from os.path import join, split, exists, abspath, isfile
from fnmatch import fnmatch

#---------------------------------------------------------------------------- #
# Return from fw_<tech> if both fw/<tech> and fw_<tech> exists                #
#---------------------------------------------------------------------------- #
path = env.subst('${INC_ROOT}/fw/nb/api')
curr_file_path = os.getcwd()
if (os.path.exists(path) and 'fw_nb' in curr_file_path) or 'USES_STANDALONE_FW' in env:
  Return()

# --------------------------------------------------------------------------- #
# Import SCons Environment                                                    #
# --------------------------------------------------------------------------- #
env = env.Clone()

#-------------------------------------------------------------------------------
# Necessary Public & Restricted API's
#-------------------------------------------------------------------------------

env.RequirePublicApi(['RFLM'], area='RF')

env.RequirePublicApi([
    'DAL',
    'DEBUGTOOLS',
    'MPROC',
    'SERVICES',
    'SYSTEMDRIVERS',
    'MEMORY',
    'KERNEL',          # needs to be last
    ], area='core')

env.RequirePublicApi(['MCS'], area='MCS')
env.RequirePublicApi(['RFLM'], area='RFLM')
env.RequirePublicApi(['RFMODEM_BOLT'], area='RFMODEM_BOLT')
env.RequirePublicApi(['NB1'], area='NB1')
env.RequirePublicApi(['COMMON'], area='FW')
#-------------------------------------------------------------------------------
# Autodetect UMID files
#-------------------------------------------------------------------------------
path,tech_folder = split(Dir('../').path)
if 'fw_' in tech_folder:
  api_folder = 'fw_nb/api/'
else:
  api_folder = 'fw/nb/api/'

api_path = env.subst('${INC_ROOT}/'+api_folder)

files = [ ]
for fname in glob(join(api_path, '*_msg.h')):
  env.PrintDebugInfo('fw', "nb : AddUMID: " + fname)
  files.append(fname)
if len(files) > 0:
  env.AddUMID('${BUILDPATH}/fw_nb.umid', files)

#-------------------------------------------------------------------------------
# Set up object path
#-------------------------------------------------------------------------------
# default:
#  internal build using normal buildpath
#  external build using normal buildpath
libname = 'fw_nb'
lib = libname + '.lib'
objname = 'nbfw_main'
obj = objname + '.o'
if 'USES_INTERNAL_BUILD' not in env:
    lib = libname + '.ext.lib'
    obj = objname + '.ext.o'
path = env.subst(join('..','bin','${SHORT_BUILDPATH}'))

if 'USES_EXCLUDE_NB1_FW' in env or 'USES_BUILD_SENSORONLY_IMAGE' in env:
    Return()
variant = 'MODEM_MODEM'
if 'USES_BUILD_PAGEONLY_IMAGE' in env or 'USES_PAGEONLY_IMAGE' in env:
    variant = 'MODEM_PAGEONLY'
    path += '_pageonly'
#find path, and corresponding binaries
if not exists(join(path,lib)):
  # This step only for eng builds
  # looks for fw libs if they are not in the default path
  env.PrintWarning('FW: searching for ' + libname + ' lib...')
  path = ''
  for dname, sdname, flist in os.walk(join('..','bin')):
    for fname in flist:
      if fnmatch(fname, libname + '*.lib'):
        # we found lib, obj must also be here
        path = dname
        lib = fname
        obj = objname + '.o'
        # obj must match lib extension
        if '.ext.' in lib:
          obj = objname + '.ext.o'
        env.PrintWarning('FW: found ' + join(path, lib))
        break
    if path != '':
      break
  if path == '' or not exists(join(path, lib)) or not exists(join(path, obj)):
    # this is an error condition
    env.PrintError('FW: ' + lib + ' or ' + obj + ' NOT found!!!')
    Exit(1)

env.AddOEMObject([variant],  join(path, obj))
env.AddOEMLibrary([variant], join(path, lib))
