#===============================================================================
#
# Bam Libs
#
# GENERAL DESCRIPTION
#    BAM Libs build script
#
# Copyright (c) 2011-2015 by QUALCOMM Technologies Inc.
# All Rights Reserved.
# QUALCOMM Proprietary/GTDR
#
#-------------------------------------------------------------------------------
#
#  $Header:  $
#  $DateTime:  $
#  $Author:  $
#  $Change:  $
#                      EDIT HISTORY FOR FILE
#
#  This section contains comments describing changes made to the module.
#  Notice that changes are listed in reverse chronological order.
#
# when       who     what, where, why
# --------   ---     ---------------------------------------------------------
# 05/18/15   SA      Added 9x07 support.
# 10/20/14   SA      Added 8952 support
# 10/01/14   SA      Added 9609 and 8208 support
# 07/10/14   PR      Added 8909 support
# 04/11/14   PR      Removed 8926 as VUnicore EOLd.
# 03/04/14   SA      Added 8936 support.
# 12/31/13   PR      Remove unsupported targets.
# 10/01/13   SA      Added 8916 support
# 05/20/13   RL      Added 8926/8962 support
# 01/25/13   SS      Added 8x10 support
# 12/09/12   SS      Added 8x26 support and devcfg support for tgtcfg.
# 11/27/12   SS      Made changes for B family binary compatibility
# 09/13/11   MK      Created
#===============================================================================
import os;
Import('env')
env = env.Clone()

#-------------------------------------------------------------------------------
# Publish Private APIs
#-------------------------------------------------------------------------------
if env['MSM_ID'] in ['9205', '9607']:
   env.PublishPrivateApi('HWENGINES_BAM', [
       "${INC_ROOT}/core/hwengines/bam/core5",
       "${INC_ROOT}/core/hwengines/bam"
   ])

#-------------------------------------------------------------------------------
# Defines
#-------------------------------------------------------------------------------

#TZOS image
if env.has_key('TZOS_IMAGE'):
    env.Append(CPPDEFINES = [ 'BAM_TZOS' ])

if env.has_key('MBA_CORE_SW'):
    env.Append(CPPDEFINES = [ 'BAM_MBA' ])
else :
    env.Append(CPPDEFINES = [ 'BAM_XML_PROPS' ])

#To enable debug messages uncomment the line below:
if not (env.has_key('BUILD_TOOL_CHAIN') or env.has_key('BUILD_BOOT_CHAIN')):
    env.Append(CPPDEFINES = ['BAM_OSAL_DEBUG'])
    env.Append(CPPDEFINES = [ 'BAM_OSAL_MSG_LEVEL=MSG_HIGH' ])

if env['MSM_ID'] in ['9205', '9607']:
    if env.has_key('BUILD_TOOL_CHAIN') or env.has_key('BUILD_BOOT_CHAIN'):
        env.Append(CPPDEFINES = [ '"BAM_TGT_CFGDATA_H=\\\"${MSM_ID}/bamtgtcfgdata_boot.h\\\""'  ])
    elif env.has_key('MODEM_PROC'):
        env.Append(CPPDEFINES = [ '"BAM_TGT_CFGDATA_H=\\\"${MSM_ID}/bamtgtcfgdata_mpss.h\\\""'  ])
    elif env.has_key('ADSP_PROC'):
        env.Append(CPPDEFINES = [ '"BAM_TGT_CFGDATA_H=\\\"${MSM_ID}/bamtgtcfgdata_lpass.h\\\""' ])
    elif env.has_key('TZOS_IMAGE'):
        env.Append(CPPDEFINES = [ '"BAM_TGT_CFGDATA_H=\\\"${MSM_ID}/bamtgtcfgdata_tz.h\\\""'  ])
    elif env.has_key('APPS_PROC'):
        env.Append(CPPDEFINES = [ '"BAM_TGT_CFGDATA_H=\\\"${MSM_ID}/bamtgtcfgdata_apss.h\\\""' ])

env.Append(CPPDEFINES = [ "BAM_PIPE_MEM_CLOCK_ID=NULL"  ])

# Bam config
if env['MSM_ID'] in ['9205', '9607']:
	if env.has_key('MODEM_PROC'):
	   env.Append(CPPDEFINES = ['BAM_MODEM_CONFIG'])
	elif env.has_key('ADSP_PROC'):
	   env.Append(CPPDEFINES = ['BAM_ADSP_CONFIG'])
	elif env.has_key('TZOS_IMAGE'):
	   env.Append(CPPDEFINES = ['BAM_TZOS_CONFIG'])
	elif env.has_key('APPS_PROC'):
	   env.Append(CPPDEFINES = ['BAM_APPS_CONFIG'])

#-------------------------------------------------------------------------------
# Source PATH
#-------------------------------------------------------------------------------
SRCPATH = "${BUILD_ROOT}/core/hwengines/bam/"

env.VariantDir('${BUILDPATH}', SRCPATH, duplicate=0)

#-------------------------------------------------------------------------------
# Internal depends within CoreBSP
#-------------------------------------------------------------------------------
CBSP_API = [
   'HAL',
   'HWENGINES',
   'SERVICES',
   'SYSTEMDRIVERS',
   'DAL',
   # needs to be last also contains wrong comdef.h
   'KERNEL',
   'POWER',
]

if env.has_key('TZOS_IMAGE'):
    CBSP_API += ['SECUREMSM']
else:
    CBSP_API += ['DEBUGTOOLS']

env.RequirePublicApi(CBSP_API)
env.RequireRestrictedApi(CBSP_API)

#-------------------------------------------------------------------------------
# Sources, libraries
#-------------------------------------------------------------------------------
if env['MSM_ID'] in ['9205', '9607']:
	BAM_COMMON_SOURCES =  [
	   '${BUILDPATH}/bam.c',
	   '${BUILDPATH}/bamosal.c',
       '${BUILDPATH}/bam_debug.c',
	   '${BUILDPATH}/core5/bamcore.c',
   ]
else:
   BAM_COMMON_SOURCES =  [
	   '${BUILDPATH}/stub/bam_stub.c',
	]

if env['MSM_ID'] in ['9205', '9607']:
    BAM_COMMON_SOURCES += ['${BUILDPATH}/${MSM_ID}/bamtgtcfg.c']

env.AddLibrary(['APPS_IMAGE', 'CBSP_APPS_IMAGE', 'MBA_CORE_SW','TZOS_IMAGE', 'QDSP6_SW_IMAGE', 'CBSP_QDSP6_SW_IMAGE',  'MODEM_PAGEONLY','MODEM_SENSORONLY'],
   '${BUILDPATH}/bam', BAM_COMMON_SOURCES)

if 'USES_RCINIT' in env:
  RCINIT_IMG = ['MODEM_IMAGE',    'CBSP_MODEM_IMAGE',  'CORE_APPS',
                'QDSP6_SW_IMAGE', 'CBSP_QDSP6_SW_IMAGE', 'MODEM_PAGEONLY','MODEM_SENSORONLY']
  env.AddRCInitFunc(
    RCINIT_IMG,
    {
      'sequence_group'             : 'RCINIT_GROUP_0',
      'init_name'                  : 'bam_drv',
      'init_function'              : 'bam_drv_init',
      'dependencies'               : ['clk_regime']
    })

#-------------------------------------------------------------------------------
# DEVCFG - BAM XML
#-------------------------------------------------------------------------------
if ('USES_DEVCFG' in env) or ('USES_DEVCFG_PAGEONLY' in env) or ('USES_DEVCFG_SENSORONLY' in env) and not (env.has_key('BUILD_TOOL_CHAIN') or env.has_key('BUILD_BOOT_CHAIN')):
   if env['MSM_ID'] in ['9205', '9607']:
      DEVCFG_IMG = ['DAL_DEVCFG_IMG','DAL_DEVCFG_PAGEONLY_IMG','DAL_DEVCFG_SENSORONLY_IMG']
      env.AddDevCfgInfo(DEVCFG_IMG,
      {
            
            '9205_xml' : ['${BUILD_ROOT}/core/hwengines/bam/9205/bam_config.xml',
                          '${BUILD_ROOT}/core/hwengines/bam/9205/bamconfig_props.c'],
            '9607_xml' : ['${BUILD_ROOT}/core/hwengines/bam/9607/bam_config.xml',
                          '${BUILD_ROOT}/core/hwengines/bam/9607/bamconfig_props.c']                          
      })
   
if env['MSM_ID'] in ['9205', '9607']:
    BAM_CONFIG_SOURCES = ['${BUILDPATH}/${MSM_ID}/bamconfig_props.c']
    env.AddLibrary(['QDSP6_SW_IMAGE', 'CBSP_QDSP6_SW_IMAGE', 'MODEM_PAGEONLY','MODEM_SENSORONLY'], '${BUILDPATH}/bam_cfg', BAM_CONFIG_SOURCES)


