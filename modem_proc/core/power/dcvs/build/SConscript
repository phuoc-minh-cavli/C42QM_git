#===============================================================================
#                    Copyright 2009-2019 QUALCOMM Incorporated.
#                           All Rights Reserved.
#                         QUALCOMM Proprietary/GTDR
#
#-------------------------------------------------------------------------------
#
#  $Header: //components/rel/core.mpss/3.10/power/dcvs/build/SConscript#10 $
#  $DateTime: 2020/03/08 23:51:40 $
#
#===============================================================================

#===============================================================================
# DCVS Libs
#-------------------------------------------------------------------------------
Import('env')
env = env.Clone()

#-------------------------------------------------------------------------------
# Target Checks
#-------------------------------------------------------------------------------
# Set CHIP target based dir path
CHIP_TARGET = env['MSM_ID']

#-------------------------------------------------------------------------------
# Determine target information
#-------------------------------------------------------------------------------
env['DCVS_TARG_CHIP'] = CHIP_TARGET
#env['DCVS_TARG_PROC'] = env.DetermineProcessorName()

#-------------------------------------------------------------------------------
# Determine if target specific files need to be used
#-------------------------------------------------------------------------------

env.Append(CPPDEFINES = [('DCVS_OS_HEADER1_H','\\"blast.h\\"')])
env.Append(CPPDEFINES = [('DCVS_OS_HEADER2_H','\\"blast.h\\"')])
env.Append(CPPDEFINES = [('DCVS_OS_HEADER3_H','\\"qurt.h\\"')])

if env['MSM_ID'] in ['9205'] and env.has_key('USES_LTE_CATM'):
  env.Append(CPPDEFINES = ['FEATURE_BYPASS_DCVS_TURBO_VOTE','DCVS_STARTUP_SVS_REQ=326']) 

#-------------------------------------------------------------------------------
# Determine which os specific files to use.
#-------------------------------------------------------------------------------
if env.has_key('USES_BREWMP'):
   env['DCVS_TARG_OS'] = 'bmp'
elif env.has_key('USES_BLAST'):
   env['DCVS_TARG_OS'] = 'blast'
else:
   env['DCVS_TARG_OS'] = 'l4'

#-------------------------------------------------------------------------------
# Source PATH
#-------------------------------------------------------------------------------
SRCPATH = "${BUILD_ROOT}/core/power/dcvs"

env.VariantDir('${BUILDPATH}', SRCPATH, duplicate=0)

#-------------------------------------------------------------------------------
# Clean Pack Rules
#-------------------------------------------------------------------------------
# Remove target specific files
ALL_TARGET_FILES = env.FindFiles(['*'], '${BUILD_ROOT}/core/power/dcvs/src/tgt/')

MY_TARGET_FILES = env.FindFiles(['*'], '${BUILD_ROOT}/core/power/dcvs/src/tgt/'+CHIP_TARGET)
env.PrintInfo("DCVS tgt directories: " + '${BUILD_ROOT}/core/power/dcvs/src/tgt/'+CHIP_TARGET + ', '.join(MY_TARGET_FILES))

PACK_TARGET_FILES = list(set(ALL_TARGET_FILES) - set(MY_TARGET_FILES))
env.CleanPack(['CORE_MODEM'], PACK_TARGET_FILES)

MY_DOC_FILES = env.FindFiles(['*.docx'], '${BUILD_ROOT}/core/power/dcvs/docs/')
PACK_DOC_FILES = list(set(MY_DOC_FILES))
env.CleanPack(['CORE_MODEM'], PACK_DOC_FILES)

MY_EXE_FILES = env.FindFiles(['*.exe'], '${BUILD_ROOT}/core/power/dcvs/tools/')
PACK_EXE_FILES = list(set(MY_EXE_FILES))
env.CleanPack(['CORE_MODEM'], PACK_EXE_FILES)

MY_FDCVS_FILES = env.FindFiles(['*.c'], '${BUILD_ROOT}/core/power/dcvs/test/')
PACK_FDCVS_FILES = list(set(MY_FDCVS_FILES))
env.CleanPack(['CORE_MODEM'], PACK_FDCVS_FILES)

MY_HDRinc_FILES = env.FindFiles(['*.h'], '${BUILD_ROOT}/core/power/dcvs/inc/')
PACK_HDRinc_FILES = list(set(MY_HDRinc_FILES))
env.CleanPack(['CORE_MODEM'], PACK_HDRinc_FILES)

MY_HDRsrc_FILES = env.FindFiles(['*.h'], '${BUILD_ROOT}/core/power/dcvs/src/')
PACK_HDRsrc_FILES = list(set(MY_HDRsrc_FILES))
env.CleanPack(['CORE_MODEM'], PACK_HDRsrc_FILES)

MY_DCVSHDR_FILES = env.FindFiles(['*.h'], '${BUILD_ROOT}/core/power/dcvs/test/')
PACK_DCVSHDR_FILES = list(set(MY_DCVSHDR_FILES))
env.CleanPack(['CORE_MODEM'], PACK_DCVSHDR_FILES)

#-------------------------------------------------------------------------------
# External depends within CoreBSP
#-------------------------------------------------------------------------------
env.RequireExternalApi([
   'BREW',
   'CS',
])

#-------------------------------------------------------------------------------
# Internal depends within CoreBSP
#-------------------------------------------------------------------------------
CBSP_API = [
   'DAL',
   'BUSES',
   'POWER',
   'MPROC',
   'SERVICES',
   'SYSTEMDRIVERS',
   'DEBUGTOOLS',
   'DEBUGTRACE',
   'STORAGE',
   # needs to be last also contains wrong comdef.h
   'KERNEL',
]

env.RequirePublicApi(CBSP_API)
env.RequireRestrictedApi(CBSP_API)

env.PublishProtectedApi('DCVS_INC', [
	"${INC_ROOT}/core/power/dcvs/inc",
 SRCPATH + "/src/common",
  SRCPATH + "/src/common/algorithms/classic",
  SRCPATH + "/src/common/algorithms/disabled",
  SRCPATH + "/src/common/algorithms/fixed",
  SRCPATH + "/src/common/algorithms/qdsp_classic",
  SRCPATH + "/src/common/algorithms/requests",
 SRCPATH + "/src/tgt/${DCVS_TARG_CHIP}/modem",
  SRCPATH + "/src/os/${DCVS_TARG_OS}",
 SRCPATH + "/src/Env/Default",
	])

env.PublishProtectedApi('CORE_API_KERNEL', [
	"${INC_ROOT}/core/api/kernel/quartz",
	"${INC_ROOT}/core/api/kernel/rex",
        "${INC_ROOT}/core/kernel/qurt/libs/qurt/include/public"
	])

env.RequireProtectedApi(['CORE_API_KERNEL'])
env.RequireProtectedApi(['POWER_UTILS'])
env.RequireProtectedApi(['DCVS_INC'])


#-------------------------------------------------------------------------------
# Sources, libraries
#-------------------------------------------------------------------------------
if env['DCVS_TARG_OS'] in ['l4']:
  DCVS_OS_LIB_SOURCES = [
    '${BUILDPATH}/src/os/${DCVS_TARG_OS}/cpu_monitor.c',
    '${BUILDPATH}/src/os/${DCVS_TARG_OS}/dcvs_os.c',
    '${BUILDPATH}/src/os/${DCVS_TARG_OS}/dcvs_nv_os.c',
    ]
else:
    DCVS_OS_LIB_SOURCES = [
    '${BUILDPATH}/src/os/${DCVS_TARG_OS}/dcvs_os.c',
    '${BUILDPATH}/src/os/${DCVS_TARG_OS}/dcvs_nv_os.c',
    ]

DCVS_LIB_SOURCES = [
   DCVS_OS_LIB_SOURCES,
   '${BUILDPATH}/src/common/dcvs_core.c',
   '${BUILDPATH}/src/common/dcvs_npa.c',
   '${BUILDPATH}/src/common/dcvs_nv.c',
   '${BUILDPATH}/src/common/mca_npa_qdsp.c',
   '${BUILDPATH}/src/common/mca_diag_hooks.c',
   '${BUILDPATH}/src/common/algorithms/disabled/disabled.c',
   '${BUILDPATH}/src/common/algorithms/classic/classic.c',
   '${BUILDPATH}/src/common/algorithms/qdsp_classic/qdsp_classic.c',
   '${BUILDPATH}/src/common/algorithms/fixed/fixed.c',
   '${BUILDPATH}/src/common/algorithms/requests/requests.c',
   '${BUILDPATH}/src/tgt/${DCVS_TARG_CHIP}/modem/dcvs_target.c',
   '${BUILDPATH}/src/tgt/${DCVS_TARG_CHIP}/modem/dcvs_clkstub.c',
   '${BUILDPATH}/src/tgt/${DCVS_TARG_CHIP}/modem/mca_target.c',
   ]

DCVS_MARKER_SOURCES = [
   '${BUILDPATH}/src/common/dcvs_marker.c',
   ]

#-------------------------------------------------------------------------------
# Add Libraries to image
#-------------------------------------------------------------------------------

env.AddBinaryLibrary(['SINGLE_IMAGE', 'CBSP_SINGLE_IMAGE'], '${BUILDPATH}/dcvs', DCVS_LIB_SOURCES)
env.AddBinaryLibrary(['QDSP6_SW_IMAGE', 'CBSP_QDSP6_SW_IMAGE'], '${BUILDPATH}/dcvs', DCVS_LIB_SOURCES)

# Mdm9k the Modem builds as APPS_IMAGE and Modem Controller builds as MODEM_IMAGE
# so we are mapping the Modem Controller to the marker implementation.

env.AddBinaryLibrary(['MODEM_IMAGE', 'CBSP_MODEM_IMAGE', 'modem','MODEM_SENSORONLY'],  '${BUILDPATH}/dcvs', DCVS_LIB_SOURCES)

env.AddBinaryLibrary(['APPS_IMAGE', 'CBSP_APPS_IMAGE'],  '${BUILDPATH}/dcvs', DCVS_LIB_SOURCES)

env.AddBinaryLibrary(['CORE_GSS'], '${BUILDPATH}/dcvs', DCVS_LIB_SOURCES)

#-------------------------------------------------------------------------------
# Register with RCINIT framework so that it can call our init functions.
#-------------------------------------------------------------------------------
if 'USES_RCINIT' in env:
  RCINIT_IMG = ['CORE_MODEM', 'CORE_QDSP6_SW', 'CORE_GSS','MODEM_SENSORONLY']
  env.AddRCInitFunc(
    RCINIT_IMG,
    {
      'sequence_group'             : 'RCINIT_GROUP_0',
      'init_name'                  : 'core_cpu',
      'init_function'              : 'CoreCpuInitialize',
      'dependencies'               : ['npa','icbarb']
    })
  env.AddRCInitFunc(
    RCINIT_IMG,
    {
      'sequence_group'             : 'RCINIT_GROUP_6',
      'init_name'                  : 'dcvs',
      'init_function'              : 'DCVSInitialize',
      'dependencies'               : ['npa','fs','core_cpu'],
      'policy_optin'               : ['default', 'ftm', ],
   })

#-------------------------------------------------------------------------------
# Register SW events with the QDSS Tracer framework.
#--------------------------------------------------------------------------
if 'USES_QDSS_SWE' in env:
  env.Append(CCFLAGS = " -DDCVS_TRACER_SWEVT")
  QDSS_IMG = ['QDSS_EN_IMG']
  events = [['DCVS_CPU_FREQUENCY_UPDATE', 'DCVS Update: CPU clock=%lu kHz bandwidth=%lu Mbps'],
            ['DCVS_CPU_UTILIZATION_UPDATE', 'DCVS CPU utilization update']]
  env.AddSWEInfo(QDSS_IMG, events)

if 'QDSS_TRACER_SWE' in env:
  env.SWEBuilder(['${BUILDPATH}/dcvs_tracer_event_ids.h'],None)
  env.Append(CPPPATH = ['${BUILD_ROOT}/core/power/dcvs/build/$BUILDPATH'])
