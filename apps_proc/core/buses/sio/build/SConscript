#===============================================================================
#
# SIO Libs
#
# GENERAL DESCRIPTION
#    build script
#
# Copyright (c) 2009-2016 by QUALCOMM, Incorporated.
# All Rights Reserved.
# QUALCOMM Proprietary/GTDR
#
#-------------------------------------------------------------------------------
#
#  $Header: //components/rel/core.tx/6.0/buses/sio/build/SConscript#1 $
#  $DateTime: 2020/01/30 22:49:35 $
#  $Author: pwbldsvc $
#  $Change: 22400605 $
#                      EDIT HISTORY FOR FILE
#
#  This section contains comments describing changes made to the module.
#  Notice that changes are listed in reverse chronological order.
#
# when       who     what, where, why
# --------   ---     ---------------------------------------------------------
#11/03/16    rc      Included the data mux public flag
#===============================================================================
Import('env')
env = env.Clone()
env.Append(CPPDEFINES=['MSG_BT_SSID_DFLT=MSG_SSID_SIO'])

#-------------------------------------------------------------------------------
# Source PATH
#-------------------------------------------------------------------------------

SRCPATH = "${BUILD_ROOT}/core/buses/sio/src"

env.VariantDir('${BUILDPATH}', SRCPATH, duplicate=0)

#-------------------------------------------------------------------------------
# External depends outside CoreBSP
#-------------------------------------------------------------------------------

#  Use a common list of APIs across all targets.  If an API identifier doesn't
#  exist on a specific target, it's silently ignored.

CBSP_XAPI = [
    'CS',
    'DSM',
    'MODEM_API',
    'MODEM_DATA',
    'MODEM_HDR',
    'MODEM_LTE',
    'MODEM_PMIC',
    'MODEM_RF',
    'MODEM_SERVICES',
    'MULTIMEDIA_AUDIO',
    'WCONNECT',
    'WIRELESSCONNECTIVITY',
]

env.RequireExternalApi( CBSP_XAPI )

#-------------------------------------------------------------------------------
# Internal depends within CoreBSP
#-------------------------------------------------------------------------------

CBSP_API = [
    'BOOT',
    'DAL',
    'DEBUGTOOLS',
    'HAL',
    'HWENGINES',
    'MEMORY',
    'MPROC',
    'POWER',
    'QBI',
    'SERVICES',
    'STORAGE',
    'SYSTEMDRIVERS',
    'WIREDCONNECTIVITY',
	'BUSES',

    # needs to be last also contains wrong comdef.h
    'KERNEL',
]

env.RequirePublicApi( CBSP_API )
env.RequireRestrictedApi( CBSP_API )
env.RequirePublicApi( 'QBI', area='mbim' )
env.RequirePublicApi( 'ATFWD_EXT', area='dataservices' )
#-------------------------------------------------------------------------------
# Source Code and LIBs
#-------------------------------------------------------------------------------

SIO_SOURCES =  [
   '${BUILDPATH}/common/sio.c',
   '${BUILDPATH}/amss/sio_wrapper.c'
]

sio_obj = env.Object(SIO_SOURCES)
sio_lib = env.Library('${BUILDPATH}/sio', sio_obj)

#-------------------------------------------------------------------------------
# Add Libraries to image
#-------------------------------------------------------------------------------

if env['MSM_ID'] in ['6615','6695']:

    env.AddLibsToImage(['SINGLE_IMAGE', 'CBSP_SINGLE_IMAGE'], sio_lib )

elif env['MSM_ID'] in ['7x30']:

    env.AddLibsToImage(['MODEM_IMAGE', 'CBSP_MODEM_IMAGE', 'APPS_IMAGE', 'CBSP_APPS_IMAGE'], sio_lib )

elif env['MSM_ID'] in ['9x00']:

    env.AddLibsToImage(['MODEM_IMAGE', 'CBSP_MODEM_IMAGE', 'APPS_IMAGE', 'CBSP_APPS_IMAGE', 'QDSP6_SW_IMAGE', 'CBSP_QDSP6_SW_IMAGE'], sio_lib )

else:

    #  Everything else (8660, 8960, 8064, 8974, 9x15, etc.).  We don't want to add a case for each target.

    env.AddLibsToImage(['MODEM_IMAGE', 'CBSP_MODEM_IMAGE', 'APPS_IMAGE', 'CBSP_APPS_IMAGE', 'CORE_QDSP6_SW'], sio_lib )

#---------------------------------------------------------------------------
# Setup the constants for document title and the DCN number for Agile
#---------------------------------------------------------------------------
env['DOCTITLE']="SIO API"
env['DOCREVISION']="80-N1875-1"

DOCSRCPATH = "${BUILD_ROOT}/core/buses/api/sio"
env.Replace(DOCSRCPATH = DOCSRCPATH)

#---------------------------------------------------------------------------
# List of Header files that contain doxygen tags   List of TeX files that
# contain documentation to be merged with doxygen generated TeX files
#---------------------------------------------------------------------------
docsrcfiles = [
    '${DOCSRCPATH}/docsrc/sio/sio_MainPage.h',
    '${DOCSRCPATH}/sio.h',
    '${DOCSRCPATH}/docsrc/sio/introduction.tex',
    ]

#---------------------------------------------------------------------------
# Call the builder with the name of the document. The document can
# be found under \core\api\doc once the build completes.
#---------------------------------------------------------------------------
if env['MSM_ID'] in ['7x30']:
    target_doc = env.PdfDocument('SIO_API',
                                 docsrcfiles
                                )

#---------------------------------------------------------------------------
# Finally add the document to the core docs target.
#---------------------------------------------------------------------------
if env['MSM_ID'] in ['7x30']:
    env.AddDocs('CORE_DOCS',target_doc)

#---------------------------------------------------------------------------
# RCInit framework.
#---------------------------------------------------------------------------

if 'USES_RCINIT' in env:
   RCINIT_IMG = ['CORE_MODEM', 'CORE_QDSP6_SW', 'APPS_IMAGE', 'CBSP_APPS_IMAGE']
   env.AddRCInitFunc(           # Code Fragment in TMC: NO
    RCINIT_IMG,                 # define TMC_RCINIT_INIT_SIO_INIT
    {
     'sequence_group'             : 'RCINIT_GROUP_0',                   # required
     'init_name'                  : 'sio',                              # required
     'init_function'              : 'sio_init',                         # required
     'dependencies'               : ['GPIOInt','time','timer_init']
    })

